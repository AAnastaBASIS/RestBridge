use java.io.File
use java.util.HashMap
use java.util.Iterator

use ::RestBridgeConfigurator.bbj::RestBridgeConfigurator
use ::ContextSelectionDialog.bbj::ContextSelectionDialog
use ::AdminAPICredentialWindow.bbj::AdminAPICredentialWindow
use ::RestBridgeConfigurationWindow.bbj::RestBridgeConfigurationWindow

print 'hide'

declare BBjAdmin admin!
declare AdminAPICredentialWindow credentialsWindow!

admin! = BBjAPI().getAdmin("admin", "admin123", err=*next)
if(admin! = null()) then
    credentialsWindow! = new AdminAPICredentialWindow()
    sysGui! = BBjAPI().openSysGui("X0")
    
    while(admin! = null())
        credentialsWindow!.open(sysGui!)
        if(credentialsWindow!.dialogCancelled()) then
            release
        endif
        
        admin! = BBjAPI().getAdmin(credentialsWindow!.getUsername(), credentialsWindow!.getPassword())
    wend
endif

declare HashMap contextMap!
declare BBjVector mappings!
declare BBjVector configurations!
declare RestBridgeConfigurator configurator!

configurator! = new RestBridgeConfigurator(admin!)

rem get the context names where the Rest Bridge has been configured
contextNames! = configurator!.getRestBridgeContextConfigurations()

if(!contextNames!.isEmpty()) then
    answer = msgbox("Select what you want to do.",7,"Rest Bridge Configuration","Create","Edit","Remove")

    rem Cancel
    if(answer < 1) then
        release
    endif

    rem filling the context map<ContextName, BBjVector<Mapping>>
    contextMap! = new HashMap()
    for i=0 to contextNames!.size() -1
        contextName! = contextNames!.get(i)
        mappings! = configurator!.getRestBridgeMappingsForContext(contextName!)
        contextMap!.put(contextName!, mappings!)
    next i

    if(answer > 1) then
        deleteMode = 0
        if(answer = 3) then
            if(REV < "REV 18.00") then
                a = msgbox("In BBj Versions prior to 18.00 the BBJSP could only be removed manually in the Enterprise Manager.")
                release
            endif
            deleteMode = 1
        else
            editMode = 1
        endif

        declare ContextSelectionDialog dialog!
        
        if(contextMap!.size() = 1 AND mappings!.size() = 1) then
            contextMapping! = mappings!.get(0)
        else
            dialog! = new ContextSelectionDialog(deleteMode)
            dialog!.setContextMap(contextMap!)
            dialog!.show()
    
            if(dialog!.cancelled()) then
                release
            endif
    
            contextName! = dialog!.getSelectedContextName()
            contextMapping! = dialog!.getSelectedMapping()
        endif
 
        if(deleteMode) then
            answer = msgbox("Do you really want to remove the Rest Bridge configuration from the selected Context(" + contextName! + ") with the selected Mapping(" + contextMapping! +") ?", 4)
            if(answer = 6) then
                configurator!.removeRestBridgeFromContext(contextName!, contextMapping!)
                answer = msgbox("The Rest Bridge configuration has been removed successfully from the selected context. Do you want to start/restart the context: " + contextName!, 4)
                if(answer = 6) then
                    configurator!.restartContext(contextName!)
                endif
            endif
            release
        endif
    endif
endif

declare RestBridgeConfigurationWindow window!
window! = new RestBridgeConfigurationWindow()

if(editMode) then
    window!.setEditMode(Boolean.TRUE)
    window!.setContextName(contextName!)
    window!.setMapping(contextMapping!)

    declare BBjspServletConfiguration conf!
    conf! = configurator!.getBBjspServlet(contextName!, contextMapping!)
    window!.setTimeout(num(conf!.getParameter("REST_TIMEOUT"), err=*next))
    window!.setWorkingDirectoryPath(conf!.getParameter("REST_WD"))
    window!.setProgramPrefix(conf!.getParameter("REST_PGM_PREFIX"))
    window!.setProgramSuffix(conf!.getParameter("REST_PGM_SUFFIX"))
    window!.setConfigFilePath(conf!.getConfig())
    window!.setAuthProgramPath(conf!.getParameter("REST_AUTHPGM"))
    window!.setUseGetAllowedFilters(num(conf!.getParameter("USE_GET_ALLOWED_FILTER"), err=*next))
    window!.setRequestLogPath(conf!.getParameter("REST_REQUESTLOG"))
else
    window!.setContextNames(configurator!.getContextNames())
    window!.setAuthProgramPath(configurator!.getDefaultAuthProgramPath())
endif

displayConfigurationWindow:

window!.show()

if(window!.cancelled()) then
    release
endif

selectedContext! = window!.getContext()
selectedMapping! = window!.getMapping()

if(!contextNames!.isEmpty()) then
    if(contextMap!.containsKey(selectedContext!)) then
        if(cast(BBjVector,contextMap!.get(selectedContext!)).contains(selectedMapping!)) then
            a = msgbox("There is already a BBJSP Servlet defined for this context and mapping, please change")
            goto displayConfigurationWindow
        endif
    endif
endif

declare HashMap params!
params! = new HashMap()
params!.put("REST_TIMEOUT", str(int(window!.getTimeout())))
params!.put("REST_WD", window!.getWorkingDirectoryPath())
params!.put("REST_AUTHPGM", window!.getAuthProgramPath())
params!.put("REST_PGM_PREFIX", window!.getProgramPrefix())
params!.put("REST_PGM_SUFFIX", window!.getProgramSuffix())
params!.put("USE_GET_ALLOWED_FILTER", str(int(window!.useGetAllowedFilters())))
params!.put("REST_REQUESTLOG", window!.getRequestLogPath())

if(editMode) then
    configurator!.editRestBridgeConfiguration(selectedContext!, selectedMapping!, params!)
    introText! = "The Rest Bridge has been successfully edited."
else
    configurator!.configureRestBridge(selectedContext!, selectedMapping!, params!)
    introText! = "The Rest Bridge has been successfully configured."
endif

answer = msgbox(introText! + " Do you want to start/restart the context: " + selectedContext!, 4)
if(answer = 6) then
    configurator!.restartContext(selectedContext!)
endif

release